PROJECT(rkward)

CMAKE_MINIMUM_REQUIRED(VERSION 2.8.12)	# As required by KF5
IF(NOT "${CMAKE_VERSION}" VERSION_LESS 3.3.0)
	CMAKE_POLICY(SET CMP0063 OLD)  # No symbol visibility in any of our static libraries needed
ENDIF()

IF(NOT CMAKE_VERBOSE_MAKEFILE)
	SET (FORCE_PRETTY_MAKEFILE ON)
ENDIF(NOT CMAKE_VERBOSE_MAKEFILE)

FIND_PACKAGE(ECM 0.0.11 REQUIRED NO_MODULE)

SET(CMAKE_MODULE_PATH ${ECM_MODULE_PATH} ${ECM_KDE_MODULE_DIR})

INCLUDE(KDEInstallDirs)
INCLUDE(KDECMakeSettings)
INCLUDE(KDECompilerSettings)
INCLUDE(ECMInstallIcons)
INCLUDE(FeatureSummary)

FIND_PACKAGE(Qt5 5.2 CONFIG REQUIRED COMPONENTS Widgets Core Xml Network WebKit Script PrintSupport)
FIND_PACKAGE(KF5 5.2 REQUIRED COMPONENTS CoreAddons DocTools I18n XmlGui TextEditor WidgetsAddons WebKit Parts Config Notifications WindowSystem)
FIND_PACKAGE(Gettext REQUIRED)
if("${CMAKE_VERSION}" VERSION_GREATER 3.2.3)
	FIND_PACKAGE(Intl REQUIRED)
ELSE()
# Note: this may or may not work (it does not on Windows), but is worth a try:
	IF(NOT ${Intl_LIBRARIES})
		SET(Intl_LIBRARIES ${GETTEXT_LIBRARIES})
	ENDIF(NOT ${Intl_LIBRARIES})
ENDIF()

IF(FORCE_PRETTY_MAKEFILE)
	SET(CMAKE_VERBOSE_MAKEFILE OFF)
ENDIF(FORCE_PRETTY_MAKEFILE)

ADD_DEFINITIONS(${QT_DEFINITIONS} -DQT_NO_CAST_TO_ASCII)
ADD_DEFINITIONS(-DQT_USE_FAST_CONCATENATION -DQT_USE_FAST_OPERATOR_PLUS)
ADD_DEFINITIONS(-DQT_NO_URL_CAST_FROM_STRING)

#uncomment the line below to save ~250-350kB in object size
#ADD_DEFINITIONS(-DRKWARD_NO_TRACE)

ADD_SUBDIRECTORY(rkward)
ADD_SUBDIRECTORY(i18n)
ADD_SUBDIRECTORY(doc)
ADD_SUBDIRECTORY(tests)

FEATURE_SUMMARY(WHAT ALL FATAL_ON_MISSING_REQUIRED_PACKAGES)
